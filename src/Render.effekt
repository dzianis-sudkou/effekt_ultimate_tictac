module src/Render

import src/lib
import tty
import io/console
import stream

def renderBoard(board: BigBoard): Unit = {
  with Formatted::formatting
  renderSeparator()
  board.foreach {elem =>  // elem is a List[SmallBoard] -> List[List[List[Cell]]]
    val bigRow: List[Cell] = createUnifiedRow(elem)
    renderRow(bigRow)
    renderSeparator()
    ()
  }
  ()
}

def renderRow(row: List[Cell]): Unit = {
  with Formatted::formatting
  var newRow: String = ""
  row.foreach {c =>
    c match {
      case Empty() => newRow = newRow ++ "·".yellow ++ " "
      case Active() => newRow = newRow ++ "◦".green ++ " "
      case Filled(player) => player match {
        case Cross() => newRow = newRow ++ "X".red ++ " "
        case Nought() => newRow = newRow ++ "O".green ++ " "
      }
      case NewLine() => newRow = newRow ++ "\n"
      case Separator() => newRow = newRow ++ "|".magenta ++ " "
    }
  }
  println(newRow)
}

def renderSeparator(): Unit = {
  with Formatted::formatting
  var separator: List[String] = fill(11, "—".magenta)
  separator = separator.replace(3, "+".magenta)
  separator = separator.replace(7, "+".magenta)
  println(separator.foldLeft("") { (acc, elem) => acc ++ elem ++ " "})
}

def createUnifiedRow(boardRow: List[SmallBoard]): List[Cell] = {
  with on[OutOfBounds].panic // disable all the error messages

  var unifiedRow: List[List[Cell]] = []

  var counterSmallBoard: Int = 0
  var counterRow: Int = 0

  while (counterRow < 3) {
    val smallBoard: SmallBoard = boardRow.get(counterSmallBoard)

    unifiedRow = unifiedRow.insert(unifiedRow.size(), smallBoard.get(counterRow)) // insert the row of the small board

    if (counterSmallBoard == 2) {
      unifiedRow = unifiedRow.insert(unifiedRow.size(), [NewLine()]) // new line symbol
      counterSmallBoard = 0
      counterRow = counterRow + 1
    } else {
      unifiedRow = unifiedRow.insert(unifiedRow.size(), [Separator()]) // insert the separator
      counterSmallBoard = counterSmallBoard + 1
    }
    ()
  }
  var resultRow: List[Cell] = unifiedRow.join()
  resultRow = resultRow.deleteAt(resultRow.size() - 1) // remove the last new line
  resultRow
}
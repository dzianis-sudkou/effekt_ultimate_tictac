module src/Game

import tty
import char
import io/console
import stream
import src/lib
import src/Render
import src/Generate
import src/Opponent


/*
 * Core Function of the Game
 * Input : None
 * Output : Unit
 */
def playGame(): Unit = {
  
  /*
   * Returns the chosen Game Mode
   * Input : None
   * Output : GameMode
   */
  def chooseGameMode():  GameMode / {WrongInput, ExitGame, PrintRules} = {
    println("Choose the Game Mode:\n[1 - Local; 2 - vs Computer;  h - Read the rules; q - Exit the game]")
    val side = consoleInput()
    side match {
      case '1' => Local()
      case '2' => Computer()
      case 'h' => do PrintRules(); chooseGameMode()
      case 'q' => do ExitGame()
      case _ =>
        do WrongInput("Invalid Game Mode. Available options: [1, 2, h, q]!")
        chooseGameMode()
    }
  }


  /*
   * Returns the Cell Type of the player
   * Input : None
   * Output : Cell
   */
  def chooseSide(): Cell / {WrongInput, ExitGame} = {
    println("Choose the side[X, O]:")
    val side = consoleInput()
    side match {
      case 'X' => Cross()
      case 'O' => Nought()
      case 'q' => do ExitGame()
      case _ =>
        do WrongInput("Invalid player type. Available options: [X, O, q]!")
        chooseSide()
    }
  }


  /*
   * Returns the Small Board Number
   * Input : gameBoard: GameBoard
   * Output : Int [0 - 8]
   */
  def chooseSmallBoard(gameBoard: GameBoard): Int / {WrongInput} = {
    println("Choose the Small Board[1 - 9]:")
    val smallBoardNumber: Char = consoleInput()

    if (isDigit(smallBoardNumber)) {
      with on[WrongFormat].panic
      val smallBoardNumber: Int = parseDigit(smallBoardNumber) - 1
      if (checkAvailableCell(gameBoard.smallCopy, smallBoardNumber)){
        smallBoardNumber
      }else {
        do WrongInput("This Small Board is Already occupied. Please type a number for a valid Small Board.")
        chooseSmallBoard(gameBoard)
      }
    }
    else {
      do WrongInput("Invalid input. Please type a digit from 1 to 9.")
      chooseSmallBoard(gameBoard)
    }
  }


  /*
   * Returns the chosen Cell inside the board
   * Input : gameBoard: GameBoard
   * Output : Int [0 - 8]
   */
  def chooseCell(gameBoard: GameBoard, smallBoardNumber: Int): Int / {WrongInput} = {
    with on[OutOfBounds].panic

    println("Choose the cell inside the chosen Small Board")
    val cellNumber: Char = consoleInput()

    if (isDigit(cellNumber)) {
      with on[WrongFormat].panic
      val cellNumberInt: Int = parseDigit(cellNumber) - 1
      if(checkAvailableCell(gameBoard.bigBoard.get(smallBoardNumber), cellNumberInt)){
        cellNumberInt
      }else{
        do WrongInput("This Cell is Already occupied. Please type a number for a valid cell.")
        chooseCell(gameBoard, smallBoardNumber)
      }
    }else {
      do WrongInput("Invalid input. Please type a number from 1 to 9.")
      chooseCell(gameBoard, smallBoardNumber)
    }
  }


  /*
   * Core Game Loop
   * Input : player: Cell, gameMode: GameMode
   * Output : Unit
   */
  def gameLoop(player: Cell, gameMode: GameMode): Unit / {WrongInput} = {
    var endGame: Bool = false

    var gameBoard: GameBoard = generateNewBigBoard()
    var currentSmallBoard: Int = 10
    var currentCell: Int = 10
    
    var computer: Cell = Cross()
    if (player is Cross()) computer = Nought()

    var currentPlayer: Cell = Cross()

    printGameScreen(gameBoard, player)

    println(playerString(currentPlayer) ++ " - You're starting the game!\nChoose the starting Small Board.")

    // INNER GAME LOOP OF ONE GAME
    while (not(endGame)){
      with on[OutOfBounds].panic
      // Choose the new Small Board
      if (currentSmallBoard == 10) {
        if (gameMode is Computer()) {
          if (isComputerTurn(currentPlayer, computer)) currentSmallBoard = computerPlay(gameBoard.smallCopy, computer)
          else currentSmallBoard = chooseSmallBoard(gameBoard)
        }else currentSmallBoard = chooseSmallBoard(gameBoard)
        gameBoard = activateSmallBoard(gameBoard, currentSmallBoard)
      }

      // The Move Phase
      printGameScreen(gameBoard, currentPlayer)
      if (gameMode is Computer()) {
        if (isComputerTurn(currentPlayer, computer)) currentCell = computerPlay(gameBoard.bigBoard.get(currentSmallBoard), computer)
        else currentCell = chooseCell(gameBoard, currentSmallBoard)
      }else currentCell = chooseCell(gameBoard, currentSmallBoard)

      gameBoard = checkNewCell(gameBoard, currentSmallBoard, currentCell, currentPlayer)

      // val finish = checkWinSituation(gameBoard.bigBoard.get(currentSmallBoard), currentPlayer) match {
      val finish: Finish = checkWinSituation(gameBoard.bigBoard.get(currentSmallBoard), currentPlayer)
      if (finish.isFinished) {
        finish.state match {
          case Win() => {
            println("\nYou won the Small Board!")
            gameBoard = fillWinningGameBoard(gameBoard, currentSmallBoard, currentPlayer)
          }
          case Lose() => {
            println("\n It's a draw in the Small Board!")
            gameBoard = fillWinningGameBoard(gameBoard, currentSmallBoard, Draw())
          }
        }
        consoleInput()
        ()
      }

      // After The Player's Move

      // Change the Current Small Board
      if (checkAvailableCell(gameBoard.smallCopy, currentCell)){
        gameBoard = deactivateSmallBoard(gameBoard, currentSmallBoard)
        currentSmallBoard = currentCell
        gameBoard = activateSmallBoard(gameBoard, currentSmallBoard)
      } else {
        gameBoard = deactivateSmallBoard(gameBoard, currentSmallBoard)
        currentSmallBoard = 10
      }
      currentCell = 10

      // Verify if the game has been won
      val finishGame: Finish = checkWinSituation(gameBoard.smallCopy, currentPlayer)
      if (finishGame.isFinished) {
        finishGame.state match {
          case Win() => println(playerString(currentPlayer) ++ ", You won the game!")
          case Lose() => println(playerString(currentPlayer) ++ ", You lost the game!")
        }
        consoleInput()
        endGame = true
      }
      // Change The Current Player
      if(currentPlayer is Cross()) currentPlayer = Nought()
      else currentPlayer = Cross()

      printGameScreen(gameBoard, currentPlayer)
    }
  }

  // MAIN SECTION
  // Greeting Message
  printWelcomeScreen()
  
  var play = true

  var gameMode: GameMode = Local()
  while (play) {
    try{
      // Starting the game
      gameLoop(chooseSide(), chooseGameMode())
    } with WrongInput { msg =>
      println(msg)
      resume(())
    } with ExitGame {
      play = false
    } with PrintRules {
      printRules()
      resume(())
    }
  }
  printExitScreen()
}


def main(): Unit = {
  playGame()
}
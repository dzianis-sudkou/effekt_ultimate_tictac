module src/Stupid

import tty
import io/console
import stream
import src/lib
import src/Render
import src/Generate



def playGame(): Unit = {

  // Returns the chosen Game Mode
  def chooseGameMode(): GameMode / {WrongInput} = {
    println("Choose the Game Mode:\n[1 - Local, 2 - vs Computer]:")
    val side = consoleInput()
    side match {
      case "1" => Local()
      case "2" => Computer()
      case _ =>
        do WrongInput("Invalid Game Mode. Please type '1' or '2'!")
        chooseGameMode()
    }
  }

  // Returns the Cell type for the side
  def chooseSide(): Cell / {WrongInput} = {
    println("Choose the side[X, O]:")
    val side = consoleInput()
    side match {
      case "X" => Cross()
      case "O" => Nought()
      case _ =>
        do WrongInput("Invalid player type. Please type 'X' or 'O'!")
        chooseSide()
    }
  }


  // Returns the Small Board Number [0 - 8]
  def chooseSmallBoard(gameBoard: BigBoards): Int / {WrongInput} = {
    println("Choose the Small Board[1 - 9]:")
    val smallBoardNumber: String = consoleInput()

    if (not(any(Numbers) {s => s == smallBoardNumber})) {
      do WrongInput("Invalid input. Please type a number from 1 to 9.")
      chooseSmallBoard(gameBoard)
    }
    else {
      with on[WrongFormat].panic
      val smallBoardNumber: Int = toInt(smallBoardNumber)
      if (checkAvailableCell(gameBoard.smallCopy, smallBoardNumber - 1)){
        smallBoardNumber - 1
      }else {
        do WrongInput("This Small Board is Already occupied. Please type a number for a valid Small Board.")
        chooseSmallBoard(gameBoard)
      }
    }
  }

  // Returns the chosen Cell inside the board [0 - 8]
  def chooseCell(gameBoard: BigBoards, smallBoardNumber: Int): Int / {WrongInput} = {
    with on[OutOfBounds].panic

    println("Choose the cell inside your Small Board")
    val cellNumber: String = consoleInput()
    if (not(any(Numbers) {s => s == cellNumber})) {
      do WrongInput("Invalid input. Please type a number from 1 to 9.")
      chooseCell(gameBoard, smallBoardNumber)
    }else {
      with on[WrongFormat].panic
      val cellNumberInt = toInt(cellNumber)

      if(checkAvailableCell(gameBoard.bigBoard.get(smallBoardNumber), cellNumberInt - 1)){
        cellNumberInt - 1
      }else{
        do WrongInput("This Cell is Already occupied. Please type a number for a valid cell.")
        chooseCell(gameBoard, smallBoardNumber)
      }
    }
  }

  def gameLoop(player: Cell, gameMode: GameMode): Unit / {WrongInput} = {
    var endGame: Bool = false

    var gameBoard: BigBoards = generateNewBigBoard()
    var currentSmallBoard: Int = 10
    var currentCell: Int = 10
    
    var computer: Cell = Cross()
    if (player is Cross()) computer = Nought()

    var currentPlayer: Cell = Cross()

    printGameScreen(gameBoard, player)

    if (player is Cross()) {
      println("You're starting the game!\nChoose the starting Small Board.")
      currentSmallBoard = chooseSmallBoard(gameBoard)
      gameBoard = newActiveSmallBoard(gameBoard, currentSmallBoard)
    } else {
      println("Opponent is starting the game!\n")
      if (gameMode is Computer()){
        currentSmallBoard = computerPlay(gameBoard.smallCopy, computer)
        gameBoard = newActiveSmallBoard(gameBoard, currentSmallBoard)
      }else{
        currentSmallBoard = chooseSmallBoard(gameBoard)
        gameBoard = newActiveSmallBoard(gameBoard, currentSmallBoard)
      }
    }

    // INNER GAME LOOP OF ONE GAME
    while (not(endGame)){
      with on[OutOfBounds].panic
      
      // Choose the new Small Board
      if (currentSmallBoard == 10) {
        if (gameMode is Computer()) if (isComputerTurn(currentPlayer, computer)) currentSmallBoard = computerPlay(gameBoard.smallCopy, computer)
        else currentSmallBoard = chooseSmallBoard(gameBoard)
        gameBoard = newActiveSmallBoard(gameBoard, currentSmallBoard)
      }

      // The Move Phase
      printGameScreen(gameBoard, currentPlayer)
      if(gameMode is Computer()) if (isComputerTurn(currentPlayer, computer)) currentCell = computerPlay(gameBoard.bigBoard.get(currentSmallBoard), computer) 
      else currentCell = chooseCell(gameBoard, currentSmallBoard)

      gameBoard = checkNewCell(gameBoard, currentSmallBoard, currentCell, currentPlayer)
      checkWinSituation(gameBoard.bigBoard.get(currentSmallBoard - 1), currentPlayer) match {
        case Win() => {
          println("\nYou won the Small Board!")
          consoleInput()
          gameBoard = fillWinningGameBoard(gameBoard, currentSmallBoard, currentPlayer)
        }
        case Loose() => {
          println("\n It's a draw in the Small Board!")
          consoleInput()
          gameBoard = fillWinningGameBoard(gameBoard, currentSmallBoard, Draw())
        }
        case _ => ()
      }

      // After The Player's Move
      printGameScreen(gameBoard, currentPlayer)
      println("Excellent Move! Now Press the Enter to finish your turn.")
      consoleInput()

      // Change the Current Small Board
      if (checkAvailableCell(gameBoard.smallCopy, currentCell)){
        gameBoard = deactivateSmallBoard(gameBoard, currentSmallBoard)
        currentSmallBoard = currentCell
        gameBoard = newActiveSmallBoard(gameBoard, currentSmallBoard)
      } else {
        gameBoard = deactivateSmallBoard(gameBoard, currentSmallBoard)
        currentSmallBoard = 10
      }
      currentCell = 10

      // Verify if the game has been won
      if (checkWinSituation(gameBoard.smallCopy, currentPlayer) is Win()){
        println(playerString(currentPlayer) ++ ", You won the game!")
        consoleInput()
        endGame = true
      }

      checkWinSituation(gameBoard.smallCopy, currentPlayer) match {
        case Win() => {
          println(playerString(currentPlayer) ++ ", You won the game!")
          consoleInput()
          endGame = true
        }
        case Loose() => {
          println(playerString(currentPlayer) ++ ", You lost the game!")
          consoleInput()
          endGame = true
        }
        case _ => ()
      }
      // Change The Current Player
      if(currentPlayer is Cross()) currentPlayer = Nought()
      else currentPlayer = Cross()
      ()
    }
  }

  // MAIN SECTION

  clearScreen()

  // Greeting Message
  println("Welcome to the ULTIMATE TIC-TAC-TOE!")
  println("Press enter to start the game.")

  consoleInput()
  
  var play: Bool = true

  var player: Cell = Nought()

  var gameMode: GameMode = Local()
  while (play) {

    try{
      // Choosing a Game Mode
      gameMode = chooseGameMode()

      // Choosing a player side
      player = chooseSide()

      // Starting the game
      gameLoop(player, gameMode)

    } with WrongInput { msg =>
      println(msg)
      resume(())
    }
  }
  println("Goodbye!")
}


def main(): Unit = {
  playGame()
}